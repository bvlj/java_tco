plugins {
  id("java")
  id("idea")
}

java {
  toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

repositories {
  mavenCentral()
}

// Dependencies: see gradle/libs.versions.toml
dependencies {
  implementation(libs.asm)
  implementation(libs.asmUtil)
  implementation(libs.asmTree)
}

jar {
  // Manifest of the agent
  manifest {
    attributes(
        "Premain-Class": "lab.agent.Agent",
        "Can-Redefine-Classes": "true",
        "Can-Retransform-Classes": "true",
    )
  }

  from {
    // Copy runtime dependencies of this module into the jar
    configurations.runtimeClasspath.collect {
      zipTree(it).matching {
        // Do not copy module-info.class of dependencies
        exclude("**/module-info.class")
      }
    }
  }
}

// Download sources and javadoc jars of dependencies
idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}
